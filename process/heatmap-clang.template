set terminal postscript eps size 6,2 enhanced color font 'Helvetica,12'
set output 'heatmap-DATASET.eps'

set palette defined (0 "white", 1 "red")

set key off

# set labels for x/y axis to block sizes"
XTICS="XTICS_DATA"
YTICS="YTICS_DATA"

set for [i=1:words(XTICS)] xtics ( word(XTICS,i) i-1 )
set for [i=1:words(YTICS)] ytics ( word(YTICS,i) i-1 )

# don't show color scale next to the heatmap
unset colorbox

set datafile separator ","
set yrange [*:*] reverse

set multiplot layout 2, 2 rowsfirst

set title "x86-64, -fno-omit-frame-pointer"

plot "DATASET-x86-64-no-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : $3) with image, \
     "DATASET-x86-64-no-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : sprintf("%g",$3)) with labels

set title "x86-64, -omit-frame-pointer"

plot "DATASET-x86-64-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : $3) with image, \
     "DATASET-x86-64-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : sprintf("%g",$3)) with labels


set title "native, -fno-omit-frame-pointer"

plot "DATASET-native-no-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : $3) with image, \
     "DATASET-native-no-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : sprintf("%g",$3)) with labels

set title "native, -omit-frame-pointer"

plot "DATASET-native-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : $3) with image, \
     "DATASET-native-omit-frame-pointer.data" matrix using 1:2:($3 == 0 ? "" : sprintf("%g",$3)) with labels
